импорт Основное

@ВПроекте
конст КОД_БАЗОВОЙ_ВАЛЮТЫ = "RUB"

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [
        ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляРуководителя(),
        новый РазрешениеДоступа(
            [новый КлючДоступаДляАутентифицированных.Объект()], 
            [Сущность.Право.Чтение])
    ]
;

@ВПроекте
метод ПолучитьВсе(): ЧитаемыйМассив<Валюты.Ссылка>
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ 
            Валюты
        ГДЕ 
            Код != %КОД_БАЗОВОЙ_ВАЛЮТЫ
    }
    
    возврат Запрос.Выполнить().Преобразовать(Строка -> Строка.Ссылка)
;

@ВПроекте
метод ПолучитьБазовуюВалюту(): Валюты.Ссылка?
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Ссылка
        ИЗ 
            Валюты
        ГДЕ 
            Код == %КОД_БАЗОВОЙ_ВАЛЮТЫ
    }
    
    возврат Запрос.Выполнить().ЕдинственныйИлиУмолчание()?.Ссылка
;

@ВПроекте
метод СоздатьБазовуюВалюту(): Валюты.Ссылка
    знч БазоваяВалюта = новый Валюты.Объект(Код = КОД_БАЗОВОЙ_ВАЛЮТЫ, Наименование = "Рубль")
    БазоваяВалюта.Записать()
    возврат БазоваяВалюта.Ссылка
;
