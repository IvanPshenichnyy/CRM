импорт Общие
импорт Основное

@Обработчик
метод ПередЗаписьюОбъекта()
    пер ПоляНеЗаполнены: Булево
    если Объект.Наименование.Сократить().Пусто()
        Компоненты.ПолеВводаНаименование.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаНаименование.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если Объект.Регион == Неопределено
        Компоненты.ПолеВводаРегион.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаРегион.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если ПоляНеЗаполнены
        выбросить новый ИсключениеВалидации("Не заполнены обязательные поля", ОтображатьСообщениеОбОшибке = Ложь)
    ;
;

метод СохранитьПриНажатии(КомандаСохранить: ОбычнаяКоманда)
    знч ЭтоНовый = ЭтоНовый()
    
    Записать()
    Закрыть(КлючОбъекта)
    
    если ЭтоНовый
        знч УведомлениеОКлиенте = новый Уведомление(
            ЛокализованныеСтроки.НовыйКлиент(),
            ЛокализованныеСтроки.УведомлениеОСозданииКлиента(Объект.Наименование)
        )
        
        УведомлениеОКлиенте.Данные = КлючОбъекта
        УведомлениеОКлиенте.СохранитьВЦентреУведомлений = Истина
        УведомлениеОКлиенте.ОценкаИнформации = ОценкаИнформации.Положительная
    
        пер КомандаОткрытия = новый КомандаСПараметром<Уведомление>(
            (Команда, Уведомление) -> КлиентыФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = Уведомление.Данные), 
            ЛокализованныеСтроки.Открыть()
        )
    
        пер Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([КомандаОткрытия])
        УведомлениеОКлиенте.Команды = Команды
        УведомлениеОКлиенте.Показать()
    ;
;