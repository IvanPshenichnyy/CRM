импорт Основное

@Обработчик
метод ВычислитьРазрешенияДоступа(): Массив<РазрешениеДоступа>
    возврат [
        ПользователиКлиентИСервер.ПолныеПраваНаСправочникДляРуководителя(),
        новый РазрешениеДоступа(
            [новый КлючДоступаДляАутентифицированных.Объект()], 
            [Сущность.Право.Чтение])
    ]
;

@ВПроекте
метод ПолучитьНачальнуюСтадию(): СтадииСделок.Ссылка?
    знч Запрос = Запрос{
         ВЫБРАТЬ ПЕРВЫЕ 1
            Ссылка КАК Стадия
         ИЗ 
            СтадииСделок
         УПОРЯДОЧИТЬ ПО 
            Порядок
    }
        
    возврат Запрос.Выполнить().ЕдинственныйИлиУмолчание()?.Стадия
;

@ВПроекте
метод ПолучитьСтадииСделок(): ЧитаемыйМассив<СтадииСделок.Ссылка>
    знч Запрос = Запрос{
         ВЫБРАТЬ
            Ссылка КАК Стадия
         ИЗ 
            СтадииСделок
         УПОРЯДОЧИТЬ ПО 
            Порядок
    }
    
    возврат Запрос.Выполнить().Преобразовать(СтрокаРезультата -> СтрокаРезультата.Стадия)
;

@ВПроекте
метод ПолучитьИконкуСтадии(Стадия: СтадииСделок.Ссылка?): ДвоичныйОбъект.Ссылка?
    знч Запрос = Запрос{
        ВЫБРАТЬ
            Иконка
        ИЗ
            СтадииСделок
        Где
            Ссылка == %Стадия
    }

    возврат Запрос.Выполнить().Единственный().Иконка
;