импорт Основное

@Обработчик
метод ПослеЧтения()
    если Клиент != Неопределено
        Объект.Владелец = Клиент
        Компоненты.ПолеВводаКлиента.Доступность = Ложь
    ;
;

@Обработчик
метод ПередЗаписьюОбъекта()
    пер ПоляНеЗаполнены: Булево
    если Объект.Наименование.Сократить().Пусто()
        Компоненты.ПолеВводаИмя.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаИмя.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если Объект.Владелец == Неопределено 
        Компоненты.ПолеВводаКлиента.РезультатПроверкиДанных = РезультатПроверкиДанных.Ошибка
        ПоляНеЗаполнены = Истина
    иначе
        Компоненты.ПолеВводаКлиента.РезультатПроверкиДанных = РезультатПроверкиДанных.Отсутствует
    ;
    если ПоляНеЗаполнены
        выбросить новый ИсключениеВалидации("Не заполнены обязательные поля", ОтображатьСообщениеОбОшибке = Ложь)
    ;
;

метод СохранитьПриНажатии(КомандаСохранить: ОбычнаяКоманда)
    знч ЭтоНовый = ЭтоНовый()

    Записать()
    Закрыть(КлючОбъекта)
    
    если ЭтоНовый
        знч УведомлениеОКонтактномЛице = новый Уведомление(
            ЛокализованныеСтроки.НовоеКонтактноеЛицо(), 
            ЛокализованныеСтроки.УведомлениеОСозданииКонтактногоЛица(Объект.Наименование)
        )
        
        УведомлениеОКонтактномЛице.Данные = КлючОбъекта
        УведомлениеОКонтактномЛице.СохранитьВЦентреУведомлений = Истина
        УведомлениеОКонтактномЛице.ОценкаИнформации = ОценкаИнформации.Положительная
        
        пер КомандаОткрытия = новый КомандаСПараметром<Уведомление>(
            (Команда, Уведомление) -> КонтактныеЛицаФормаОбъекта.ОткрытьВМодальномОкне(КлючОбъекта = Уведомление.Данные),
            ЛокализованныеСтроки.Открыть()
        )
        
        пер Команды = новый ФрагментКомандногоИнтерфейса<КомандаСПараметром<Уведомление>>([КомандаОткрытия])
        УведомлениеОКонтактномЛице.Команды = Команды
        УведомлениеОКонтактномЛице.Показать()
    ;
;