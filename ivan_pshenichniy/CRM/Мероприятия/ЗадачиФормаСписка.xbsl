импорт Общие 
импорт Основное

конст ИМЯ_ФАЙЛА_ВЫГРУЗКИ = "Задачи.xls"

метод ПриЧтенииНастроекТаблицы(Источник: Таблица<ДинамическийСписок>, Событие: СобытиеСДанными<Массив<СохраненнаяНастройкаТаблицы>>)
    знч НастройкаМоиЗадачи = новый СохраненнаяНастройкаТаблицы("Мои задачи")
    
    НастройкаМоиЗадачи.Фильтр.Элементы.Добавить(новый ЭлементФильтра(
        Поле = "Статус",
        ВидСравнения = ВидСравнения.ВСписке,
        Значение = новый Массив<СтатусМероприятия>([СтатусМероприятия.Запланировано, СтатусМероприятия.ВПроцессе]),
        Использовать = Истина
    ))
    
    знч Сотрудник = ПользователиКлиентИСервер.ПолучитьТекущегоСотрудника()
    если Сотрудник != Неопределено
        НастройкаМоиЗадачи.Фильтр.Элементы.Добавить(новый ЭлементФильтра(
            Поле = "Ответственный",
            ВидСравнения = ВидСравнения.Равно,
            Значение = Сотрудник,
            Использовать = Истина
        ))
    ;
    
    НастройкаМоиЗадачи.Сортировка.Добавить(новый ЭлементСортировки(
        Поле = "ДатаСоздания",
        НаправлениеСортировки = НаправлениеСортировки.ПоУбыванию
    ))
    
    Событие.Данные.Добавить(НастройкаМоиЗадачи)
;

@Обработчик
метод ПослеСоздания()
    ТекущийСотрудник = ПользователиКлиентИСервер.ПолучитьТекущегоСотрудника()
;

метод ОтмеченныеЗадачиВЗавершенные(Команда: КомандаСПараметром<Массив<Сущность.Ключ>>, Отмеченные: Массив<Сущность.Ключ>)
    знч Задачи = Отмеченные.Преобразовать(Задача -> Задача как Задачи.Ссылка)
    
    Компоненты.ТаблицаСписка.ОчиститьОтмеченныеСтроки()
    ЗавершитьЗадачи(Задачи)
    Компоненты.ТаблицаСписка.Обновить()
    
    знч Уведомление = новый Уведомление(ЛокализованныеСтроки.ЗавершениеЗадач(), ЛокализованныеСтроки.ЗадачиПомеченыКакЗавершенные())
    Уведомление.ОценкаИнформации = ОценкаИнформации.Положительная
    Уведомление.Показать()
;

метод ОтмеченныеЗадачиВФайл(Команда: КомандаСПараметром<Массив<Сущность.Ключ>>, Отмеченные: Массив<Сущность.Ключ>)
    знч Задачи = Отмеченные.Преобразовать(Задача -> Задача как Задачи.Ссылка)
    
    знч ДвоичнаяСсылка = МероприятияКлиентИСервер.ВыгрузитьЗадачиВExcel(Задачи)
    ВыгрузкаФайлов.НачатьВыгрузку(ДвоичнаяСсылка, ИМЯ_ФАЙЛА_ВЫГРУЗКИ)
;

метод МоиЗадачиВФайлПриНажатии(Команда: ОбычнаяКоманда)
    если ТекущийСотрудник != Неопределено
        знч ДвоичнаяСсылка = МероприятияКлиентИСервер.ВыгрузитьЗадачиСотрудникаВExcel(ТекущийСотрудник)
        ВыгрузкаФайлов.НачатьВыгрузку(ДвоичнаяСсылка, ИМЯ_ФАЙЛА_ВЫГРУЗКИ)
    ;
;

@НаСервере @ДоступноСКлиента
статический метод ЗавершитьЗадачи(Задачи: Массив<Задачи.Ссылка>)
    для Задача из Задачи
        знч ЗадачаОбъект = Задача.ЗагрузитьОбъект()
        если ЗадачаОбъект.Статус != СтатусМероприятия.Завершено
            ЗадачаОбъект.Завершить()
        ;
    ;
;